apply plugin: "com.android.application"
if (!project.hasProperty("xmake")) {
    apply plugin: "com.getkeepsafe.dexcount"
}
apply from: "../git.gradle"

def packageName="com.sap.sailing.racecommittee.app"
def verCode=1

android {
    buildToolsVersion rootProject.ext.buildTools
    compileSdkVersion rootProject.ext.compileSdk

    compileOptions {
        sourceCompatibility rootProject.ext.sourceCompatibilityVersion
        targetCompatibility rootProject.ext.targetCompatibilityVersion
    }

    /**
     * Copies apk into www apps directory and create version file
     */
   applicationVariants.all { variant ->
       variant.outputs.all { output ->
           def newName = outputFileName.replace(".apk", "-v" + variant.versionName + ".apk");
           newName = newName.replace("-" + variant.buildType.name, "");
           outputFileName = new File(newName)
       }
       variant.assemble.doLast { assemble ->
           // Create version file
           def versionFile = new File(rootProject.ext.destinationFolder, "$packageName." + "version")
           versionFile.text = "$packageName" + ".apk=$verCode"
           copy {
               from variant.outputs*.outputFile
               into rootProject.ext.destinationFolder
           }
       }
   }

    def versionPropertiesFile = file('project.properties')
    if (versionPropertiesFile.canRead()) {
        def versionProps = new Properties()
        versionProps.load(new FileInputStream(versionPropertiesFile))
        verCode = versionProps['VERSION_CODE'].toInteger()
        def runTasks = gradle.startParameter.taskNames
        if ('assemble' in runTasks || 'assembleRelease' in runTasks || 'aR' in runTasks) {
            verCode = verCode + 1
        }
        versionProps.store(versionPropertiesFile.newWriter(), null)
    }

    defaultConfig {
        applicationId packageName
        minSdkVersion rootProject.ext.minSdk
        targetSdkVersion rootProject.ext.targetSdk
        versionCode verCode
        versionName "$verCode - Laser"
    }

    sourceSets {
        main {
            manifest.srcFile "AndroidManifest.xml"
            java.srcDirs = ["src"]
            res.srcDirs = ["res"]
            assets.srcDirs = ["assets", "assets_gen"]
        }

        androidTest {
            java.srcDirs = ["tests-ui/src"]
            res.srcDirs = ["tests-ui/res"]
            assets.srcDirs = ["tests-ui/assets"]
        }

        test {
            java.srcDirs = ["tests-jvm/src"]
        }
    }

    signingConfigs {
        stg_debug {
            keyAlias "stg_debug"
            keyPassword "stg_debug"
            storeFile file("../android-keystore/STG-debug.jks")
            storePassword "stg_debug"
        }
    }

    buildTypes {
        debug {
            versionNameSuffix ".debug"
            signingConfig signingConfigs.stg_debug

            minifyEnabled false
            proguardFiles getDefaultProguardFile("proguard-android.txt"), "proguard-project.txt", "../proguard-mobile.txt"
        }

        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile("proguard-android.txt"), "proguard-project.txt", "../proguard-mobile.txt"
        }
    }

    packagingOptions {
        exclude "LICENSE.txt"
        exclude ".readme"
        exclude "pom.properties"
        exclude "pom.xml"
    }
}

dependencies {
    /* support libraries */
    implementation "com.android.support:recyclerview-v7:${rootProject.ext.support}"

    /* local dependencies */
    implementation project(":mobile:com.sap.sailing.android.shared")

    /* JVM tests */
    testImplementation "junit:junit:${rootProject.ext.junit}"
    testImplementation "org.mockito:mockito-core:${rootProject.ext.mockito}"
}

// Custom tasks

/**
 * Deletes apk and version from www apps directory
 */
clean.doLast {
    ant.delete() {
        fileset(dir: file("../../java/com.sap.sailing.www/apps"), includes: "$packageName*")
    }
}
