apply plugin: "com.android.application"
apply plugin: 'com.getkeepsafe.dexcount'
apply from: "../git.gradle"

def packageName="com.sap.sailing.android.tracking.app"
def verCode=1

android {
    buildToolsVersion rootProject.ext.buildTools
    compileSdkVersion rootProject.ext.compileSdk

    defaultConfig {
        applicationId packageName
        minSdkVersion rootProject.ext.minSdk
        targetSdkVersion rootProject.ext.targetSdk
        versionCode verCode
        versionName "$verCode - Altitude"
        useJack false // not working on Windows yet
    }

    sourceSets {
        main {
            manifest.srcFile "AndroidManifest.xml"
            java.srcDirs = ["src"]
            res.srcDirs = ["res"]
            assets.srcDirs = ["assets", "assets_gen"]
        }

        androidTest {
            java.srcDirs = ["tests-ui/src"]
            res.srcDirs = ["tests-ui/res"]
            assets.srcDirs = ["tests-ui/assets"]
        }

        test {
            java.srcDirs = ["tests-jvm/src"]
        }
    }

    signingConfigs {
        stg_debug {
            keyAlias "stg_debug"
            keyPassword "stg_debug"
            storeFile file("../android-keystore/STG-debug.jks")
            storePassword "stg_debug"
        }
    }

    buildTypes {
        debug {
            versionNameSuffix ".debug"
            signingConfig signingConfigs.stg_debug

            minifyEnabled false
            proguardFiles getDefaultProguardFile("proguard-android.txt"), "proguard-project.txt", "../proguard-mobile.txt"
        }

        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile("proguard-android.txt"), "proguard-project.txt", "../proguard-mobile.txt"
        }
    }

    packagingOptions {
        exclude "LICENSE.txt"
        exclude "META-INF/maven/com.googlecode.json-simple/json-simple/pom.properties"
        exclude "META-INF/maven/com.googlecode.json-simple/json-simple/pom.xml"
    }
}

dependencies {
    /* support libraries */
    compile "com.android.support:appcompat-v7:${rootProject.ext.support}"

    /* Google Play Services */
    compile "com.google.android.gms:play-services-gcm:${rootProject.ext.play_services}"
    compile "com.google.android.gms:play-services-location:${rootProject.ext.play_services}"

    /* license dialog */
    compile("de.psdev.licensesdialog:licensesdialog:${rootProject.ext.license_dialog}")

    /* local dependencies */
    compile project(":mobile:com.sap.sailing.android.shared")

    /* JVM tests */
    testCompile "junit:junit:${rootProject.ext.junit}"
    testCompile "org.mockito:mockito-core:${rootProject.ext.mockito}"

//    /* UI testing with espresso */
//    androidTestCompile "com.android.support:support-annotations:${rootProject.ext.support}"
//    androidTestCompile "com.android.support.test:runner:${rootProject.ext.test}"
//    androidTestCompile "com.android.support.test:rules:${rootProject.ext.test}"
//    androidTestCompile "com.android.support.test.espresso:espresso-core:${rootProject.ext.espresso}"
//    androidTestCompile ("com.android.support.test.espresso:espresso-contrib:${rootProject.ext.espresso}") {
//        exclude group: "com.android.support", module: "appcompat"
//        exclude group: "com.android.support", module: "support-v4"
//        exclude module: "recyclerview-v7"
//    }
}

// Custom tasks

/**
 * Deletes apk and version from www apps directory
 */
clean.doLast {
    ant.delete() {
        fileset(dir: file("../../java/com.sap.sailing.www/apps"), includes: "$packageName*")
    }
}

/**
 * Copies apk into www apps directory and create version file
 */
build.doLast {
    // Copy APK file
    copy {
        from("build/outputs/apk") {
            include "**/*debug.apk"
        }
        into "../../java/com.sap.sailing.www/apps"
        rename { String fileName -> fileName.replace("-debug", "") }
    }

    // Create version file
    def versionFile = file("../../java/com.sap.sailing.www/apps/$packageName." + "version")
    versionFile.text = "$packageName" + ".apk=$verCode"
}
